plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

repositories {
    mavenLocal()
    mavenCentral()
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// local.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("local.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load keystore properties from local.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    signingConfigs {
        LeapSample {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "is.leap.android.sample"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 2
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {

        debug {
            ext.enableCrashlytics = true
            applicationIdSuffix ".debug"
            minifyEnabled false
        }

        release {
            ext.enableCrashlytics = true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.LeapSample
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.1'
    implementation 'is.leap.android.creator:sdk:1.0.0-beta1'
    implementation 'is.leap.android.aui:sdk:1.0.0-beta1'
}